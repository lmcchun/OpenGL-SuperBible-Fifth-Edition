// ADS 点光照着色器
// 顶点着色器

#version 130

// 输入每个顶点, 位置和法向
in vec4 vVertex;
in vec3 vNormal;

// Set per batch
uniform vec4 ambientColor;
uniform vec4 diffuseColor;
uniform vec3 specularColor;
uniform vec3 vLightPosition;
uniform mat4 mvpMatrix;
uniform mat4 mvMatrix;
uniform mat3 normalMatrix;

// 片段程序颜色
smooth out vec4 vVaryingColor;

void main(void)
{
	// 获取表面法线的视觉坐标
	vec3 vEyeNormal = normalMatrix * vNormal;
	// 获取顶点位置的视觉坐标
	vec4 vPosition4 = mvMatrix * vVertex;
	vec3 vPosition3 = vPosition4.xyz / vPosition4.w;
	// 获取到光源的向量
	vec3 vLightDir = normalize(vLightPosition - vPosition3);
	// 从点乘积得到漫反射强度
	float diffuse = max(0.0, dot(vEyeNormal, vLightDir));
	// 用强度乘以漫反射颜色, 将 alpha 值设为 1.0
	vVaryingColor.rgb = diffuse * diffuseColor.rgb;
	vVaryingColor.a = diffuseColor.a;
	// 添加环境光
	vVaryingColor += ambientColor;
	// 镜面光
	vec3 vReflection = normalize(reflect(-vLightDir, vEyeNormal));
	float spec = max(0.0, dot(vEyeNormal, vReflection)); // vEyeNormal 是否应该换成观察方向?
	if (diffuse != 0)
	{
		float fSpec = pow(spec, 128.0);
		vVaryingColor.rgb += specularColor * fSpec;
	}
	// 不要忘记对多边形进行变换
	gl_Position = mvpMatrix * vVertex;
}